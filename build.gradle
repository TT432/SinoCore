plugins {
    id 'eclipse'
    id 'maven-publish'
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.11.0-SNAPSHOT" apply false
}

apply plugin: 'java'
apply plugin: 'architectury-plugin'   // Parchment mappings.
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'maven-publish'
apply plugin: 'dev.architectury.loom'

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    silentMojangMappingsLicense()

    forge {
        dataGen {
            mod 'sinocore'
        }
        mixinConfig "sinocore.mixins.json"
    }

    launches {
        data {
            arg "--existing", file("src/main/resources").absolutePath
        }
    }
}

version = '1.18.2-1.2.1'
group = 'games.moegirl.sinocraft.sinocore' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'sinocore'

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') +
        ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ')' +
        ' Arch: ' + System.getProperty('os.arch'))

sourceSets {
    api {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }

    main {
        resources {
            // Include resources generated by data generators.
            srcDir 'src/generated/resources'
        }

        compileClasspath += api.output
        runtimeClasspath += api.output
    }
}

repositories {
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:1.18.2"
    forge "net.minecraftforge:forge:1.18.2-40.1.0"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.18.2:2022.05.22@zip")
    }
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "sinocore",
                "Specification-Vendor"    : "SinoCraft Project Team",
                "Specification-Version"   : "4",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "MoeGirl Games",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "sinocore.mixins.json"
        ])
    }

    archiveClassifier.set('')
    from(sourceSets.api.output)
}

tasks.create('apiJar', Jar) {
    dependsOn(apiClasses)
    archiveClassifier.set('api')
    from(sourceSets.api.output)
    finalizedBy(remapJar)
}

tasks.create('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from(sourceSets.main.allJava)
    from(sourceSets.api.allJava)
}

tasks.create('apiSourcesJar', Jar) {
    archiveClassifier.set('api-sources')
    from(sourceSets.api.allJava)
}

// run apiJar before build
tasks.build.dependsOn(tasks.apiJar)

// Example configuration to allow publishing using the maven-publish plugin
// This is the preferred method to reobfuscate your jar file
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
// publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact remapJar
            artifact apiJar
            artifact sourcesJar
            artifact apiSourcesJar
        }
    }
    repositories {
        mavenLocal()
        maven {
            url "file://${project.projectDir}/../mavenLocal"
        }
    }
}
